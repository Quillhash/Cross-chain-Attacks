# Cross-Chain Attack Vectors ğŸŒ‰ğŸ›¡ï¸

`Note`: These vectors are related to the protocols which integrates with LayerZero.
<br><br>**If you find any vectors missing, you can create a pull request and be a contributor of the project.**


## Introduction to Cross-Chain-Bridge

As the Web3 ecosystem grows more diverse and decentralized, applications, assets, and user activity are spreading across multiple blockchain networks. While this multi-chain paradigm offers scalability and specialization, it also creates fragmentationâ€”isolated blockchains cannot natively communicate with each other. This limits the flow of value and information across the broader ecosystem.

Cross-chain bridgesÂ are the infrastructure that solves this problem and enables the transfer of assets and data between different blockchain networks. It acts as a secure connection between two or more chains, allowing tokens and information to move across them without relying on centralized intermediaries.


These bridges play a crucial role in enabling blockchainÂ interoperability, where isolated networks become part of a larger, interconnected ecosystem. By locking, burning, minting, or unlocking tokens across chains and optionally passing arbitrary messages cross-chain bridges unlock a wide range of use cases in decentralized finance (DeFi), NFTs, gaming, and more.


![Cross-Chain Bridge](image-8.png)



## Deep Diving into the attack vectors

## 1. Incorrect Refund Address in `_lzSend`

>ğŸ“Œ When using LayerZero's `_lzSend`, passing `payable(msg.sender)` from an internal contract call like `_sendMessage()` refunds excess fees to the intermediate contract  instead of the original user.<br>This causes users to lose refundable LayerZero fees, creating an unfair economic burden.

Protocols integrating LayerZero often use the internal `_lzSend` method for cross-chain messaging. A typical pattern looks like this:

1. A public `sendMessage` function in `ContractA` is called by the user to send some data for cross-chain messaging.
2. `sendMessage` then calls an `_sendMessage` function in `ContractB`, which is restricted to only be called by `ContractA`.
3. `_sendMessage` calls `_lzSend()` and passes `payable(msg.sender)` as the refund address.

Example Code: 

```javascript
function _sendMessage(
    bytes memory _data,
    uint32 _destId,
    uint256 _lzFee,
    address refundAddress 
) external override payable OnlyContractA {
    MessagingReceipt memory receipt = _lzSend(
        _destId,
        _data,
        optionsDestId[_destId],
        MessagingFee(_lzFee, 0),
        payable(msg.sender) // âŒ Incorrect: refunds go to ContractA
    );
}

```

In this flow,Â msg.senderÂ insideÂ _sendMessageÂ refers toÂ ContractA,Â not the original userÂ who initiated the call. This means any excess LayerZero fee (if unused) isÂ refunded to the contract, not the user.

As a result, usersÂ pay more than necessaryÂ for cross-chain messaging andÂ lose the refundable portionÂ of the LayerZero fee â€” creating an unfair economic burden.

>To prevent this, we have to refactor the architecture so that theÂ original userâ€™s address is passed explicitlyÂ as a parameter to the refund address inÂ _lzSend.<br> Then, inÂ ContractA pass msg.sender Â asÂ refundAddressÂ when callingÂ sendMessage().




## 2. Improper Fee and Dust Handling in OFT Transfers Can Lead to Value Loss

>ğŸ“Œ When using LayerZeroâ€™s OFT standard, overlooking the interaction betweenÂ fee deduction and decimal conversionÂ can lead toÂ silent token lossâ€”especially with small transfers. The default implementation appliesÂ dust removal before fees, and then converts to shared decimals (usually 6), which can round user balances down to zero.



When implementing LayerZero's OFT (Omnichain Fungible Token) standard, pay special attention to the decimal conversion mechanism, particularly when combined with fee implementations.

The OFT standard was designed for cross-chain token transfers between different blockchain virtual machines.

The default OFT implementation from LayerZero has a concept of local decimals and shared decimals:

- Local decimals: Specific to each chain (often 18 decimals for EVM chains)
- Shared decimals: Hardcoded in OFTCore to 6 decimals

This creates aÂ [decimalConversionRate](https://github.com/LayerZero-Labs/LayerZero-v2/blob/943ce4a2bbac070f838e12c7fd034bca6a281ccf/packages/layerzero-v2/evm/oapp/contracts/oft/OFTCore.sol#L56)Â (typically 10^12 for tokens with 18 local decimals) that can cause funds to be lost when improperly implemented, especially when combined with fee mechanisms.

Letâ€™s see the  following vulnerable [code](https://github.com/windhustler/audits/blob/21bf9a13cc8a4bfd463f055f197ec7f2511030e9/solo/PING-Security-Review.pdf):
1. The dust is removed from the given user input amount.

For example: 
```javascript
amountToSend = 1500000000000000001
sharedAmount = amountToSend / 10^12 = 1500000;
amountSentLD = sharedAmount * 10^12 = 1500000000000000000;

```
so, dust removed  from the input is 1 wei.

2. feeAmount is calculated based on the percentage and this feeAmount is subtracted from the amountSentLD.

```javascript
function _debitView(
	uint256 _amountLD,
	uint256 _minAmountLD,
	uint32 /*_dstEid*/
	) internal view virtual override returns (uint256 amountSentLD,
uint256 amountReceivedLD) {
	// @dev Remove the dust so nothing is lost on the conversion
  //between chains with different decimals for the token.
	amountSentLD = _removeDust(_amountLD);
	
 // Calculate the fee amount based on the percentage
	uint256 feeAmount = (amountSentLD * _feeNumerator) / (
FEE_DENOMINATOR * 100);

 // Deduct the fee amount from the amount to be sent
 amountReceivedLD = amountSentLD - feeAmount;

 // @dev Check for slippage.
 if (amountReceivedLD < _minAmountLD) {
 revert SlippageExceeded(amountReceivedLD, _minAmountLD);
 }
 }
```

But here is the catch:  The  above implementation doesnâ€™t care about the following execution flow.

After the [_debit](https://github.com/LayerZero-Labs/LayerZero-v2/blob/943ce4a2bbac070f838e12c7fd034bca6a281ccf/packages/layerzero-v2/evm/oapp/contracts/oft/OFTCore.sol#L181)  function call, there is another call happening to the [_buildMsgAndOptions](https://github.com/LayerZero-Labs/LayerZero-v2/blob/943ce4a2bbac070f838e12c7fd034bca6a281ccf/packages/layerzero-v2/evm/oapp/contracts/oft/OFTCore.sol#L189). 
In this flow, the amount is changed from local decimals to shared decimals.

```javascript
    function _buildMsgAndOptions(
        SendParam calldata _sendParam,
        uint256 _amountLD
    ) internal view virtual returns (bytes memory message, bytes memory options) {
        bool hasCompose;
        // @dev This generated message has the msg.sender encoded into the payload so the remote knows who the caller is.
        (message, hasCompose) = OFTMsgCodec.encode(
            _sendParam.to,
    @>>>   _toSD(_amountLD),
            // @dev Must be include a non empty bytes if you want to compose, EVEN if you dont need it on the remote.
            // EVEN if you dont require an arbitrary payload to be sent... eg. '0x01'
            _sendParam.composeMsg
        );
        // @dev Change the msg type depending if its composed or not.
        uint16 msgType = hasCompose ? SEND_AND_CALL : SEND;
        // @dev Combine the callers _extraOptions with the enforced options via the OAppOptionsType3.
        options = combineOptions(_sendParam.dstEid, msgType, _sendParam.extraOptions);

        // @dev Optionally inspect the message and options depending if the OApp owner has set a msg inspector.
        // @dev If it fails inspection, needs to revert in the implementation. ie. does not rely on return boolean
        if (msgInspector != address(0)) IOAppMsgInspector(msgInspector).inspect(message, options);
    } 
    
     /**
     * @dev Internal function to convert an amount from local decimals into shared decimals.
     * @param _amountLD The amount in local decimals.
     * @return amountSD The amount in shared decimals.
     */
    function _toSD(uint256 _amountLD) internal view virtual returns (uint64 amountSD) {
        return uint64(_amountLD / decimalConversionRate);
    }
    
     /**
     * @dev Internal function to handle the receive on the LayerZero endpoint.
     * @param _origin The origin information.
     *  - srcEid: The source chain endpoint ID.
     *  - sender: The sender address from the src chain.
     *  - nonce: The nonce of the LayerZero message.
     * @param _guid The unique identifier for the received LayerZero message.
     * @param _message The encoded message.
     * @dev _executor The address of the executor.
     * @dev _extraData Additional data.
     */
    function _lzReceive(
        Origin calldata _origin,
        bytes32 _guid,
        bytes calldata _message,
        address /*_executor*/, // @dev unused in the default implementation.
        bytes calldata /*_extraData*/ // @dev unused in the default implementation.
    ) internal virtual override {
        // @dev The src sending chain doesnt know the address length on this chain (potentially non-evm)
        // Thus everything is bytes32() encoded in flight.
        address toAddress = _message.sendTo().bytes32ToAddress();
        // @dev Credit the amountLD to the recipient and return the ACTUAL amount the recipient received in local decimals
        uint256 amountReceivedLD = _credit(toAddress, _toLD(_message.amountSD()), _origin.srcEid);

        if (_message.isComposed()) {
            // @dev Proprietary composeMsg format for the OFT.
            bytes memory composeMsg = OFTComposeMsgCodec.encode(
                _origin.nonce,
                _origin.srcEid,
                amountReceivedLD,
                _message.composeMsg()
            );

            // @dev Stores the lzCompose payload that will be executed in a separate tx.
            // Standardizes functionality for executing arbitrary contract invocation on some non-evm chains.
            // @dev The off-chain executor will listen and process the msg based on the src-chain-callers compose options passed.
            // @dev The index is used when a OApp needs to compose multiple msgs on lzReceive.
            // For default OFT implementation there is only 1 compose msg per lzReceive, thus its always 0.
            endpoint.sendCompose(toAddress, _guid, 0 /* the index of the composed message*/, composeMsg);
        }

        emit OFTReceived(_guid, _origin.srcEid, toAddress, amountReceivedLD);
    }
    
     /**
     * @dev Internal function to convert an amount from shared decimals into local decimals.
     * @param _amountSD The amount in shared decimals.
     * @return amountLD The amount in local decimals.
     */
    function _toLD(uint64 _amountSD) internal view virtual returns (uint256 amountLD) {
        return _amountSD * decimalConversionRate;
    }
    
    
```

Letâ€™s  examine the fault implementation with the given example.

1. User initiates transfer of 1,000,000,000,000 tokens (1 * 10^12).
2. There is  no dust to remove as amount is exactly 10^12
3. Fee calculation: 1,000,000,000,000 * 0.01 = 10,000,000,000 tokens (with _feeNumerator as 100).
4. Amount after fee: 1,000,000,000,000 - 10,000,000,000 = 990,000,000,000 tokens
5. Conversion to shared decimals: 990,000,000,000 Ã· 10^12 = 0.99 (rounds down to 0)
6. Amount received on destination chain: 0 * 10^12 = 0 tokens
7.  User loses all 1,000,000,000,000 tokens with nothing received


>To avoid these, we can   apply fees first, then remove dust to ensure valid amounts and add explicit checks to prevent transfers that would result in zero tokens on the destination chain.



## **3. Risk of Silent Value Truncation When OverridingÂ sharedDecimals**




>ğŸ“Œ When overriding the defaultÂ sharedDecimalsÂ (default is 6) inÂ OFTCore.solÂ to a higher value like 18, be cautious: theÂ _toSD()Â function converts amounts usingÂ uint64, whichÂ silently truncates large valuesÂ beyond ~1.84e19. This truncationÂ does not revertÂ and can result in users unintentionally losingÂ up to 92% of their tokensÂ during cross-chain transfers.


When integrating with LayerZero'sÂ OFTCore.solÂ contract, protocols must be aware of a critical limitation when overriding the defaultÂ sharedDecimalsÂ value. It's crucial to understand the behaviour of theÂ _toSDÂ function and how it interacts withÂ sharedDecimals.

TheÂ OFTCore.solÂ contract uses a defaultÂ [sharedDecimals](https://github.com/LayerZero-Labs/LayerZero-v2/blob/943ce4a2bbac070f838e12c7fd034bca6a281ccf/packages/layerzero-v2/evm/oapp/contracts/oft/OFTCore.sol#L83)Â value ofÂ 6.  If this value is overridden (e.g., set to 18), theÂ **`_toSD`**Â function may truncate large amounts due to aÂ **`uint64`**Â cast during decimal conversion.


```javascript

    constructor(uint8 _localDecimals, address _endpoint, address _delegate) OApp(_endpoint, _delegate) {
        if (_localDecimals < sharedDecimals()) revert InvalidLocalDecimals();
        decimalConversionRate = 10 ** (_localDecimals - sharedDecimals());
    }
     //...code 
     /**
     * @dev Internal function to convert an amount from local decimals into shared decimals.
     * @param _amountLD The amount in local decimals.
     * @return amountSD The amount in shared decimals.
     */
    function _toSD(uint256 _amountLD) internal view virtual returns (uint64 amountSD) {
        return uint64(_amountLD / decimalConversionRate);
    }
		//...code 

```

IfÂ decimalConversionRate == 1Â (i.e., bothÂ localDecimalsÂ andÂ sharedDecimalsÂ are set to 18), then:

- AnyÂ _amountLDÂ greater thanÂ uint64.maxÂ (â‰ˆ 1.84e19) willÂ **silently truncate**Â to fitÂ uint64.
- This can causeÂ **unexpected loss of value**Â during transfers.
- The user may attempt to transfer a large amount thinking it will succeed, but only the lower 64-bit portion will be processed.

![Chisel](image-9.png)

 Letâ€™s see the above issue with an example:

Assume the following configuration:

localDecimals = 18

sharedDecimals = 18Â â†’ soÂ decimalConversionRate = 10^(localDecimals - sharedDecimals) = 1

FunctionÂ _toSD()Â becomes:

```javascript
function _toSD(uint256 _amountLD) internal view virtual returns (uint64 amountSD) {
    return uint64(_amountLD); // since decimalConversionRate = 1
}

```

Letâ€™s say a user tries to transfer  2e20  (i.e., _amountLD =  200,000,000,000,000,000,000)

This is a validÂ `uint256`, but in the _toSD function, the following execution is done

```javascript
_toSD(2e20) = uint64(2e20) = 2e20 % 2^64 = 2e20 % 18,446,744,073,709,551,616
           â‰ˆ 2e20 - (10 * 1.844674407e19)
           â‰ˆ 200,000,000,000,000,000,000 - 184,467,440,737,095,516,160
           â‰ˆ 15,532,559,262,904,483,840

```

SoÂ instead of sending 200e18, the contract only recognizesÂ ~15.5e18Â â€” nearlyÂ 92% loss.

>This leads to the transfer executes withÂ **no revert** and users mayÂ **lose funds**Â due to unnoticed truncation.


## 4. Missing Â receive()Â Function for Refund Address


>ğŸ“Œ When usingÂ msg.valueÂ in LayerZeroâ€™sÂ _lzSend, always ensure the specifiedÂ _refundAddressÂ can accept native token refunds â€” this meansÂ implementing aÂ receive()Â functionÂ if the contract is its own refund address. Without it, excess fees cannot be refunded, leading toÂ transaction revertsÂ even if everything else is correct. If a contract is used as the refund address, addÂ receive() external payable {}Â and a secure withdrawal mechanism to recover leftover fees.


When a contract inherits fromÂ layer zero contracts Â and uses itself (or another contract) as the refund address in LayerZero'sÂ _lzSend function, the refund address contractÂ mustÂ implement aÂ receive()Â function to handle native token refunds. Without this function, excess fees cannot be refunded, causing transactions to revert.

Letâ€™s understand the above pattern with some code:

```javascript
contract MyOApp is OApp {
    function sendMessage(uint32 _dstEid, bytes memory _payload) external payable {
        _lzSend(
            _dstEid,
            _payload,
            _options,
            MessagingFee(msg.value, 0),
            payable(address(this)) // âŒ This contract as refund address
        );
    }
    
    // âŒ Missing receive() function - refunds will fail!
}
```


>If the intention is to take the excess fees through the smart contract  then always implementÂ `receive() external payable {}Â `in contracts that serve as LayerZero refund addresses, whether directly or through proxy patterns. <br>More importantly, donâ€™t forget to implement a mechanism to withdraw the excess fee which is sent to the contract only by the authorised addresses.


## 5. Insufficient Gas Limit forÂ lzReceive()Â Execution


>ğŸ“Œ One of the most critical aspects of LayerZero integration is ensuring adequate gas limits for cross-chain message execution. Insufficient gas limits will causeÂ lzReceive()Â calls to fail on the destination chain, requiring expensive retry operations and degrading user experience.


In LayerZero,Â lzReceive()Â is a crucial function thatÂ handles incoming messages on the destination chain.Â When a message is sent from one chain to another chain  using LayerZero, the destination chain's Endpoint callsÂ lzReceive()Â on the target contract, which then decodes and processes the message data.Â 

When sending cross-chain messages via LayerZero, developers must specify gas limits for the execution ofÂ lzReceive() on the destination chain through theÂ optionsÂ parameter. 

A common mistake is underestimating the actual gas consumption of the destination chain operations, leading to out-of-gas errors.

Consider the following [example](https://solodit.cyfrin.io/issues/h-02-lzreceive-call-for-releaseoneid-results-in-oog-error-pashov-audit-group-none-nftmirror_2024-12-30-markdown):

```javascript
    uint128 private constant _BASE_OWNERSHIP_UPDATE_COST = 80_000;
    (...)
    uint128 private constant _INCREMENTAL_OWNERSHIP_UPDATE_COST = 20_000; 
    (...)
    function getSendOptions(uint256[] calldata tokenIds) public pure returns (bytes memory) {
        uint128 totalGasRequired =
            _BASE_OWNERSHIP_UPDATE_COST + (_INCREMENTAL_OWNERSHIP_UPDATE_COST * uint128(tokenIds.length));

        return OptionsBuilder.newOptions().addExecutorLzReceiveOption(totalGasRequired, 0);
    }
```

The calculation allocates only 20,000 gas per token ID but  each shadow NFTÂ mintÂ operation alone costs ~46,700 gas. WithÂ transfers, itâ€™s ~27,300 gas. When aÂ transfer validatorÂ is used, it increases even further due to validation logic. 

This leads to theÂ lzReceive()Â call consistently fails with out-of-gas errors, forcing users to manually retry with higher gas limits.

## 6. Mint / Burn Pattern with ERC-4626 Vaults



>ğŸ“Œ Using theÂ Mint/BurnÂ model for cross-chain ERC-4626 token transfers canÂ artificially inflate share valueÂ on the source chain. This allows users who remain on the original chain toÂ redeem more than their fair share, effectively draining value from cross-chain participants.


When implementing cross-chain token transfers using theÂ LayerZero OFT (Omnichain Fungible Token)Â standard, developers must decide how to handle token accounting on the source and destination chains. The two common approaches are:

- **Lock/Unlock**: Tokens are locked on the source chain and unlocked on the destination
- **Mint/Burn**: Tokens are burned on the source chain and minted on the destination

WhileÂ Mint/BurnÂ is often convenient for fungible tokens, it introduces subtle butÂ critical issues when applied to ERC-4626 vaults. ERC-4626 vaults calculate share value asÂ assets / totalSupply. 

>If shares areÂ burnedÂ during cross-chain transfers,Â totalSupplyÂ decreases, which inflates the value of the remaining shares on the source chain. This creates opportunities forÂ value extractionÂ andÂ loss of fundsÂ for users transferring between chains.

Example: [1](https://solodit.cyfrin.io/issues/users-may-lose-value-when-transferring-erc-4626-vault-tokens-cross-chain-cyfrin-none-d-markdown)


## 7. Out of Gas (OOG) Error in lzReceive Function


>ğŸ“Œ When a message is processed viaÂ lzReceive(), LayerZeroâ€™s internalÂ _clearPayload()Â function attempts to update theÂ lazyInboundNonceÂ by iterating through all unprocessed nonces up to the current one.<br><br>If a high-nonce message (e.g., nonce 10,000) is received while the last processed nonce is low (e.g., 1), the function will loop through thousands of intermediate nonces. This can lead to anÂ out-of-gas (OOG) errorÂ during execution.


TheÂ lzReceiveÂ function may revert with an "out of gas" (OOG) error due to excessive gas consumption during theÂ [_clearPayload](https://github.com/LayerZero-Labs/LayerZero-v2/blob/592625b9e5967643853476445ffe0e777360b906/packages/layerzero-v2/evm/protocol/contracts/MessagingChannel.sol#L134-L142)Â execution. This occurs when processing a message with a nonce significantly higher than the currentÂ lazyInboundNonceÂ for the given receiver, source chain (srcEid), and sender. 

TheÂ _clearPayloadÂ function loops through all nonces fromÂ currentNonce + 1Â up to the incoming message's nonce to validate their payload hashes. If the gap betweenÂ currentNonceÂ and the incoming nonce is large, the loop may exceed the gas limit, causing a transaction failure.

>This error can be [mitigated](https://github.com/windhustler/Interoperability-Protocol-Security-Checklist/blob/main/audit-checklists/LayerZeroV2.md#lzreceive-function-can-revert-with-an-out-of-gas-oog-error) by processing  messagesÂ in strict nonce orderÂ (lowest nonce first) to incrementÂ lazyInboundNonceÂ incrementally. <br> This ensures the loop inÂ _clearPayloadÂ only iterates once per message, avoiding gas exhaustion.

## 8. EnforceÂ fromÂ andÂ msg.senderÂ checks inÂ lzComposeÂ implementations


>ğŸ“Œ TheÂ lzComposeÂ function is executedÂ asynchronouslyÂ by LayerZeroâ€™s EndpointV2 after a message is received. If you do not strictly validate: <br>  `from`: Must be theÂ original contractÂ (OFT) that calledÂ sendCompose <br> `msg.sender`: Must be theÂ official LayerZero EndpointV2 contract<br>thenÂ malicious composed messages will be executed by the unauthorised addresses.


Consider the default [OFT](https://github.com/LayerZero-Labs/LayerZero-v2/blob/592625b9e5967643853476445ffe0e777360b906/packages/layerzero-v2/evm/oapp/contracts/oft/OFTCore.sol#L236) implementation in LayerZero:

```javascript
//OFTCore.sol
function _lzReceive(...) internal virtual override {

    //...
    address toAddress = _message.sendTo().bytes32ToAddress();
   @>>uint256 amountReceivedLD = _credit(toAddress, _toLD(_message.amountSD()), _origin.srcEid);

    if (_message.isComposed()) {
        bytes memory composeMsg = OFTComposeMsgCodec.encode(...);
   @>>  endpoint.sendCompose(toAddress, _guid, 0, composeMsg);
    }
    
    //...
}

```

1. Tokens are credited toÂ `toAddress`.
2. Then,Â `sendCompose`Â is called toÂ queueÂ the composed message for a later call toÂ `lzCompose`.

```javascript
//MessagingComposer.sol

function sendCompose(address _to, bytes32 _guid, uint16 _index, bytes calldata _message) external {
    // must have not been sent before
    if (composeQueue[msg.sender][_to][_guid][_index] != NO_MESSAGE_HASH) revert Errors.LZ_ComposeExists();
@>>    composeQueue[msg.sender][_to][_guid][_index] = keccak256(_message);
    emit ComposeSent(msg.sender, _to, _guid, _index, _message);
}

function lzCompose(
    address _from,
    address _to,
    bytes32 _guid,
    uint16 _index,
    bytes calldata _message,
    bytes calldata _extraData
) external payable {
    // assert the validity
    bytes32 expectedHash = composeQueue[_from][_to][_guid][_index];
    bytes32 actualHash = keccak256(_message);
    if (expectedHash != actualHash) revert Errors.LZ_ComposeNotFound(expectedHash, actualHash);

    // marks the message as received to prevent reentrancy
    // cannot just delete the value, otherwise the message can be sent again and could result in some undefined behaviour
    // even though the sender(composing Oapp) is implicitly fully trusted by the composer.
    // eg. sender may not even realize it has such a bug
@>>    composeQueue[_from][_to][_guid][_index] = RECEIVED_MESSAGE_HASH;
    ILayerZeroComposer(_to).lzCompose{ value: msg.value }(_from, _guid, _message, msg.sender, _extraData);
    emit ComposeDelivered(_from, _to, _guid, _index);
}
```

`sendCompose`Â records the hashÂ usingÂ msg.senderÂ as the origin (usually the OFT contract).

Later,Â `lzCompose`Â is expected to comeÂ only from the EndpointV2, andÂ must matchÂ the originalÂ from.

>If a contract incorrectly trusts theÂ `lzCompose`Â input without verifying: <br> ThatÂ `from`Â is indeed the expected OFT contract.<br> ThatÂ `msg.sender`Â is theÂ LayerZero endpoint, not an arbitrary address...<br>This leads to the execution of composed messages by the unauthorised addresses.


## 9. **Validate All Critical Parameters in Cross-Chain Messages**


>ğŸ“Œ In LayerZero-based protocols, once the message is verified by theÂ endpoint,Â anyone can callÂ lzReceiveÂ with arbitraryÂ msg.value. If your contract [logic](https://solodit.cyfrin.io/issues/bridgedgovernorlzreceive-can-be-executed-with-different-msgvalue-than-intended-cantina-none-drips-pdf)  depends on the amount of ETH forwarded,Â not validatingÂ msg.valueÂ against an expected valueÂ opens the door to frontrunning attacks.


Consider the following [example](https://solodit.cyfrin.io/issues/bridgedgovernorlzreceive-can-be-executed-with-different-msgvalue-than-intended-cantina-none-drips-pdf):

```javascript
// BridgedGovernor.sol

function lzReceive(
			Origin calldata origin,
			bytes32, /* guid */
			bytes calldata message,
			address, /* executor */
			bytes calldata /* extraData */
) public payable onlyProxy {
			require(msg.sender == endpoint, "Must be called by the endpoint");
			require(origin.srcEid == ownerEid, "Invalid message source chain");
			require(origin.sender == owner, "Invalid message sender");
			require(origin.nonce == _lastNonce + 1, "Invalid message nonce");
			// slither-disable-next-line events-maths
			_lastNonce = origin.nonce;
			runCalls(abi.decode(message, (Call[])));
}
```

In the above logic, 

1. The contract does not validateÂ msg.valueÂ against any expected value.
2. TheÂ EndpointV2.lzReceiveÂ function is publicly callable after verification.
3. A malicious actor can front-run the legitimate executor and supply a differentÂ msg.value.
4. This can cause downstream logic inÂ runCalls()Â to behave differently than intended.

>This can be mitigated by encoding the expectedÂ `msg.value`Â as part of the message payload on the source chain. On the destination, decode it and validate it againstÂ `msg.value`Â before executing any logic.



## 10. Avoid Irrecoverable Deadlocks from Strict Ordered Execution in LayerZero

>ğŸ“Œ If your contract uses `lzReceive` with strict nonce enforcement, then a single malformed or failing message can permanently block all future messages.



When using LayerZero with ordered execution (via nextNonce), the receiving contract strictly enforces that messages must be executedÂ in the exact order of their nonce. IfÂ any message fails to executeâ€”due to malformed payloads, invalid sub-calls, gas issues, or unexpected errorsâ€”it causes aÂ hard blockÂ in the message flow:Â no future messages will be processedÂ until the failed one is resolved. 

Letâ€™s take some [buggy](https://solodit.cyfrin.io/issues/non-executable-messages-in-bridgedgovernor-can-result-in-an-unrecoverable-state-cantina-none-drips-pdf) example:

In the following BridgedGovernor contract, theÂ `lzReceive`Â function enforces strictly ordered messages by requiring the nonce to be exactlyÂ lastNonce + 1. 

If a message with malformed data  is received, it will fail during executionâ€”but the nonce will still be locked, preventing all subsequent valid messages from being processed.

```javascript
require(origin.nonce == _lastNonce + 1, "Invalid message nonce");
runCalls(abi.decode(message, (Call[]))); // Fails if message is malformed

```

Once this malformed message is received,Â the entire LayerZero message pipeline becomes stuck, and sinceÂ `BridgedGovernor`Â upgrades itself via LZ messages, it cannot recover or patch the logic.

**Remediation**:
>For most use cases, prefer the default un-ordered execution mode.<br>If you need ordered execution, then implement it at the application level rather than relying on LayerZero nonces


## 11.  Ensure LayerZero Read Functions Never Revert (to Avoid Blocking Subsequent Messages)


>ğŸ“Œ When using LayerZero Read functionality, any revert in the data reading process (inÂ readCount,Â lzMap, orÂ lzReduceÂ functions) can block the verification of the entire message. 

This creates a chain reaction where subsequent messages with higher nonces cannot be processed until the problematic message is either successfully verified or explicitly skipped.


When usingÂ LayerZero Read, ifÂ any read functionÂ likeÂ readCount,Â lzMap, orÂ lzReduceÂ reverts, the DVNs cannot generate a response. This preventsÂ message verification, leaving the assignedÂ nonce stuck. As a [result](https://github.com/windhustler/Interoperability-Protocol-Security-Checklist/blob/main/audit-checklists/LayerZeroV2.md#reverts-while-reading-data-blocks-subsequent-messages),Â subsequent messages cannot be processed, because LayerZero enforcesÂ ordered message deliveryÂ viaÂ lazyInboundNonce.

This means a single failed read (e.g., due to bad onchain logic or unhandled edge cases) canÂ block your entire LayerZero Read message queueÂ until that message is either Successfully verified or Explicitly skippedÂ usingÂ `EndpointV2::skip()`

**Prevention:**

>While theÂ **`skip`**Â function provides a way to unblock your message pipeline, it should not be used as a primary solution.
Focus on ensuring your read operations are robust and handle all edge cases gracefully to prevent verification failures in the first place.



## If you are building the protocol which integrates LayerZero, connect with the QuillAudits team to secure your protocol.

| Platform  | Links                    |
|-----------|---------------------------------|
| Telegram  | @QuillAudits                    |
| Twitter   | https://x.com/quillaudits_ai    |
| Website   | https://www.quillaudits.com/    |
| Email     | hello@quillaudits.com           |





### References:

[LayerZeroV2 Security Checklist](https://github.com/windhustler/Interoperability-Protocol-Security-Checklist/blob/main/audit-checklists/LayerZeroV2.md)
<br> [Solodit](https://solodit.cyfrin.io/?i=HIGH%2CMEDIUM)
